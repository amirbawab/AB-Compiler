prog            -> classDeclRpt progBody
classDeclRpt    -> classDecl classDeclRpt | EPSILON
classDecl       -> 'T_CLASS' classIden classBody semicolon
classIden       -> 'T_IDENTIFIER'
classBody       -> 'T_OPEN_CURLY' varDeclRptFunc closeCurly
closeCurly      -> 'T_CLOSE_CURLY'
semicolon       -> 'T_SEMICOLON'
varDeclRptFunc  -> type 'T_IDENTIFIER' varFuncDisamb | EPSILON
varFuncDisamb   -> arraySizeRpt semicolon varDeclRptFunc | 'T_OPEN_PAREN' fParams 'T_CLOSE_PAREN' funcBody semicolon funcDefRpt
funcDefRpt      -> type 'T_IDENTIFIER' 'T_OPEN_PAREN' fParams 'T_CLOSE_PAREN' funcBody semicolon funcDefRpt | EPSILON
arraySizeRpt    -> arraySize arraySizeRpt | EPSILON
arraySize       -> 'T_OPEN_SQUARE' arraySizeInt arraySizeCloSq
arraySizeInt	-> 'T_INTEGER'
arraySizeCloSq	-> 'T_CLOSE_SQUARE'
type            -> 'T_INT_TYPE' | 'T_FLOAT_TYPE' | 'T_IDENTIFIER'
fParams         -> type 'T_IDENTIFIER' arraySizeRpt fParamsTailRpt | EPSILON
fParamsTail     -> 'T_COMMA' type 'T_IDENTIFIER' arraySizeRpt
fParamsTailRpt  -> fParamsTail fParamsTailRpt | EPSILON
progBody        -> 'T_PROGRAM' funcBody semicolon funcDefRpt
funcBody        -> 'T_OPEN_CURLY' varDeclRptSt closeCurly
varDeclRptSt    -> 'T_INT_TYPE' 'T_IDENTIFIER' arraySizeRpt semicolon varDeclRptSt 
                | 'T_FLOAT_TYPE' 'T_IDENTIFIER' arraySizeRpt semicolon varDeclRptSt 
                | 'T_IDENTIFIER' varDeclRptStDis
                | 'T_IF' 'T_OPEN_PAREN' expr 'T_CLOSE_PAREN' 'T_THEN' statBlock 'T_ELSE' statBlock semicolon statementRpt
                | 'T_FOR' 'T_OPEN_PAREN' type 'T_IDENTIFIER' assignOp expr semicolon relExpr semicolon assignStat 'T_CLOSE_PAREN' statBlock semicolon statementRpt
                | 'T_GET' 'T_OPEN_PAREN' variable 'T_CLOSE_PAREN' semicolon statementRpt
                | 'T_PUT' 'T_OPEN_PAREN' expr 'T_CLOSE_PAREN' semicolon statementRpt
                | 'T_RETURN' 'T_OPEN_PAREN' expr 'T_CLOSE_PAREN' semicolon statementRpt
                | EPSILON
varDeclRptStDis -> 'T_IDENTIFIER' arraySizeRpt semicolon varDeclRptSt | idnestRptIdDiIn assignOp expr semicolon statementRpt
statementRpt    -> statement statementRpt | EPSILON
statement       -> 'T_IDENTIFIER' idnestRptIdDiIn assignOp expr semicolon
                | 'T_IF' 'T_OPEN_PAREN' expr 'T_CLOSE_PAREN' 'T_THEN' statBlock 'T_ELSE' statBlock semicolon
                | 'T_FOR' 'T_OPEN_PAREN' type 'T_IDENTIFIER' assignOp expr semicolon relExpr semicolon assignStat 'T_CLOSE_PAREN' statBlock semicolon
                | 'T_GET' 'T_OPEN_PAREN' variable 'T_CLOSE_PAREN' semicolon
                | 'T_PUT' 'T_OPEN_PAREN' expr 'T_CLOSE_PAREN' semicolon
                | 'T_RETURN' 'T_OPEN_PAREN' expr 'T_CLOSE_PAREN' semicolon
expr            -> arithExpr exprDisamb 
exprDisamb      -> relOp arithExpr | EPSILON
arithExpr       -> term arithExprRpt
arithExprRpt    -> addOp term arithExprRpt | EPSILON
relExpr         -> arithExpr relOp arithExpr
statBlock       -> 'T_OPEN_CURLY' statementRpt closeCurly | statement | EPSILON
relOp           -> 'T_GREATER_THAN' | 'T_LESS_THAN' | 'T_LESS_OR_EQUAL' | 'T_GREATER_OR_EQUAL' | 'T_IS_NOT_EQUAL' | 'T_IS_EQUAL'
assignOp        -> 'T_ASSIGN'
assignStat      -> variable assignOp expr
variable        -> 'T_IDENTIFIER' idnestRptIdDiIn
idnestRptIdDiFa -> indiceRpt idnestRptIdDFD
                | 'T_OPEN_PAREN' aParams 'T_CLOSE_PAREN'
idnestRptIdDFD  -> 'T_DOT' 'T_IDENTIFIER' idnestRptIdDiFa | EPSILON
idnestRptIdDiIn -> indiceRpt indiceRptDis
indiceRptDis    -> 'T_DOT' 'T_IDENTIFIER' idnestRptIdDiIn | EPSILON
indiceRpt       -> 'T_OPEN_SQUARE' arithExpr 'T_CLOSE_SQUARE' indiceRpt | EPSILON
sign            -> 'T_PLUS' | 'T_MINUS'
term            -> factor termRpt
termRpt         -> multOp factor termRpt | EPSILON 
factor          -> 'T_IDENTIFIER' idnestRptIdDiFa
                | 'T_INTEGER'
                | 'T_FLOAT'
                | 'T_OPEN_PAREN' arithExpr 'T_CLOSE_PAREN'
                | 'T_NOT' factor
                | sign factor
aParams         -> expr aParamsTailRpt | EPSILON
aParamsTailRpt  -> aParamsTail aParamsTailRpt | EPSILON
aParamsTail     -> 'T_COMMA' expr
addOp           -> 'T_PLUS' | 'T_MINUS' | 'T_OR'
multOp          -> 'T_MULTIPLY' | 'T_DIVIDE' | 'T_AND'