prog            -> #createGlobalTable# classDeclRpt progBody #parent#
classDeclRpt    -> classDecl classDeclRpt | EPSILON
classDecl       -> 'T_CLASS' classIden #createClassTableAndEntry# classBody semicolon #parent#
classIden       -> 'T_IDENTIFIER'
classBody       -> 'T_OPEN_CURLY' varDeclRptFunc closeCurly
closeCurly      -> 'T_CLOSE_CURLY'
semicolon       -> 'T_SEMICOLON'
varDeclRptFunc  -> type #type# varFuncIden varFuncDisamb | EPSILON
varFuncIden		-> 'T_IDENTIFIER'
varFuncDisamb   -> #createVarEntry# arraySizeRpt semicolon varDeclRptFunc | #createFunctionEntryAndTable# 'T_OPEN_PAREN' fParams closeParen funcBody semicolon #parent# funcDefRpt
closeParen		-> 'T_CLOSE_PAREN'
funcDefRpt      -> type #type# funcDefIden #createFunctionEntryAndTable# openParen fParams closeParen funcBody semicolon #parent# funcDefRpt | EPSILON
funcDefIden		-> 'T_IDENTIFIER'
openParen		-> 'T_OPEN_PAREN'
arraySizeRpt    -> arraySize arraySizeRpt | EPSILON
arraySize       -> 'T_OPEN_SQUARE' #moreType# arraySizeInt #moreType# arraySizeCloSq #moreType#
arraySizeInt	-> 'T_INTEGER'
arraySizeCloSq	-> 'T_CLOSE_SQUARE'
type            -> 'T_INT_TYPE' | 'T_FLOAT_TYPE' | 'T_IDENTIFIER'
fParams         -> type #type# fParamsIden #createParamEntry# arraySizeRpt fParamsTailRpt | EPSILON
fParamsIden		-> 'T_IDENTIFIER'
fParamsTail     -> 'T_COMMA' type #type# fParamsIden #createParamEntry# arraySizeRpt
fParamsTailRpt  -> fParamsTail fParamsTailRpt | EPSILON
progBody        -> 'T_PROGRAM' #createProgramEntryAndTable# funcBody semicolon #parent# funcDefRpt
funcBody        -> 'T_OPEN_CURLY' varDeclRptSt closeCurly
varDeclRptSt    -> 'T_INT_TYPE' #type# identifier #createVarEntry# arraySizeRpt semicolon varDeclRptSt
                | 'T_FLOAT_TYPE' #type# identifier #createVarEntry# arraySizeRpt semicolon varDeclRptSt
                | 'T_IDENTIFIER' varDeclRptStDis
                | 'T_IF' openParen expr closeParen then statBlock else statBlock semicolon statementRpt
                | 'T_FOR' #createForTable# openParen type #type# identifier #createVarEntry# assignOp expr semicolon relExpr semicolon assignStat closeParen statBlock semicolon #parent# statementRpt
                | 'T_GET' openParen variable closeParen semicolon statementRpt
                | 'T_PUT' openParen expr closeParen semicolon statementRpt
                | 'T_RETURN' openParen expr closeParen semicolon statementRpt
                | EPSILON
then			-> 'T_THEN'
else			-> 'T_ELSE'
identifier		-> 'T_IDENTIFIER'               
varDeclRptStDis -> #type# 'T_IDENTIFIER' #createVarEntry# arraySizeRpt semicolon varDeclRptSt | #useVar# idnestRptIdDiIn assignOp expr semicolon statementRpt
statementRpt    -> statement statementRpt | EPSILON
statement       -> 'T_IDENTIFIER' #useVar# idnestRptIdDiIn assignOp expr semicolon
                | 'T_IF' openParen expr closeParen then statBlock else statBlock semicolon
                | 'T_FOR' #createForTable# openParen type #type# identifier #createVarEntry# assignOp expr semicolon relExpr semicolon assignStat closeParen statBlock semicolon #parent#
                | 'T_GET' openParen variable closeParen semicolon
                | 'T_PUT' openParen expr closeParen semicolon
                | 'T_RETURN' openParen expr closeParen semicolon
expr            -> arithExpr exprDisamb 
exprDisamb      -> relOp arithExpr | EPSILON
arithExpr       -> term arithExprRpt
arithExprRpt    -> addOp term arithExprRpt | EPSILON
relExpr         -> arithExpr relOp arithExpr
statBlock       -> 'T_OPEN_CURLY' statementRpt closeCurly | statement | EPSILON
relOp           -> 'T_GREATER_THAN' | 'T_LESS_THAN' | 'T_LESS_OR_EQUAL' | 'T_GREATER_OR_EQUAL' | 'T_IS_NOT_EQUAL' | 'T_IS_EQUAL'
assignOp        -> 'T_ASSIGN'
assignStat      -> variable assignOp expr
variable        -> 'T_IDENTIFIER' #useVar# idnestRptIdDiIn
idnestRptIdDiFa -> #useVar# indiceRpt idnestRptIdDFD
                | #useFunction# 'T_OPEN_PAREN' aParams closeParen
idnestRptIdDFD  -> 'T_DOT' identifier idnestRptIdDiFaU | EPSILON
idnestRptIdDiFaU -> #useVarBasedOnLastVar# indiceRpt idnestRptIdDFD
                | #useFunctionBasedOnLastVar# 'T_OPEN_PAREN' aParams closeParen
idnestRptIdDiIn -> indiceRpt indiceRptDis
indiceRptDis    -> 'T_DOT' identifier #useVarBasedOnLastVar# idnestRptIdDiIn | EPSILON
indiceRpt       -> 'T_OPEN_SQUARE' arithExpr closeSquare indiceRpt | EPSILON
closeSquare		-> 'T_CLOSE_SQUARE'
sign            -> 'T_PLUS' | 'T_MINUS'
term            -> factor termRpt
termRpt         -> multOp factor termRpt | EPSILON 
factor          -> 'T_IDENTIFIER' idnestRptIdDiFa
                | 'T_INTEGER'
                | 'T_FLOAT'
                | 'T_OPEN_PAREN' arithExpr closeParen
                | 'T_NOT' factor
                | sign factor
aParams         -> expr aParamsTailRpt | EPSILON
aParamsTailRpt  -> aParamsTail aParamsTailRpt | EPSILON
aParamsTail     -> 'T_COMMA' expr
addOp           -> 'T_PLUS' | 'T_MINUS' | 'T_OR'
multOp          -> 'T_MULTIPLY' | 'T_DIVIDE' | 'T_AND'